SakuraMenuGreetings : all
{
	"%(time)"
	"\0"
	if open[0] != 1; "\i[4]"
	if ARRAYSIZE(currentMail) == 0; "\i[0]"
	else; "\i[2]"
	"* You look into the mailbox"
	open[0] = 1

}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	"\p[2]\s[-2]\0\b[%(RAND(8)+8)]\![set,autoscroll,disable]\_q"
	SakuraMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer

	//mail
	if ARRAYSIZE(currentMail) == 0; ", it's empty"
	elseif ARRAYSIZE(currentMail) == 1; ", there is one letter"
	else; ", there are %(ARRAYSIZE(currentMail)) letters"
	"\n\n[half]"
	for _i = 0; _i <= ARRAYSIZE(currentMail); _i++
	{
		"\__q[OnMail," + currentMail[_i] + "]\_b[img/letter (" + currentMail[_i] + ").png,inline,--option=use_self_alpha]\__q"

		if (1+_i)%5==0 && _i != 0; "\n"
	}

	"\n\n[half]"
	"\![*]\q[Mail Settings,OnSettings]\n\n[half]"
	"\![*]\q[Nevermind,OnCanel,0]"
}

OnCanel
{
	_num = TOINT(reference0)
	open[_num] = 0
	if reference0 == "0"; "\0\i[7]"
	else; "\1\i[3]"
}

OnSettings: all
{
	"\b[%(RAND(8)+8)]%(time)\![set,autoscroll,disable]\_q"
	"Mail Mode:\n\![*]"
	if mode == "1"; "\f[color,default.anchor]"
	"\__q[OnStoryMode,1]Normal\__q\f[color,default] - "
	if mode == "0"; "\f[color,default.anchor]"
	"\__q[OnStoryMode,0]Story Only\__q\f[color,default]\n\n[half]"

	"Mail Arrival:\n\![*]"
	if aitalkinterval == 30; "\f[color,default.anchor]"
	"\__q[OnTalkRateChange,30]30s,\__q\f[color,default] "
	if aitalkinterval == 60; "\f[color,default.anchor]"
	"\__q[OnTalkRateChange,60]1m,\__q\f[color,default] "
	if aitalkinterval == 180; "\f[color,default.anchor]"
	"\__q[OnTalkRateChange,180]3m,\__q\f[color,default] "
	if aitalkinterval == 300; "\f[color,default.anchor]"
	"\__q[OnTalkRateChange,300]5m,\__q\f[color,default] "
	if aitalkinterval == 600; "\f[color,default.anchor]"
	"\__q[OnTalkRateChange,600]10m\__q\f[color,default]\n\n[half]"

	"Font: \![*] \f[color,default.anchor]\__q[OnFontMenu]%(font)\__q\f[color,default]\n\n[half]"

	"Mailbox Size: \q[%(max[0]),script:\![open,sliderinput,OnMailSize,0,20,1,50\]]\n\n[half]"

	"Font Size:\n\![*]"
	"\__q[OnFontSize,+]larger\__q - "
	"\__q[OnFontSize,0]default\__q - "
	"\__q[OnFontSize,-]smaller\__q\f[color,default]\n\n[half]"
	"\![*]\q[Go Back,OnSakuraMenu]"
	" or \q[Close,OnCanel,0]"
}

OnFontMenu:all
{
	"\b[%(RAND(8)+8)]%(time)\![set,autoscroll,disable]\_qChoose font:\n\n[half]"
	"\![*]\__q[OnFontChange,default]Default Font\__q\n\n[half]"

	for _i=0;_i<ARRAYSIZE(avalibleFonts);_i++
	{
		"\![*]\__q[OnFontChange,"+avalibleFonts[_i]+"]"+avalibleFonts[_i]+"\__q\n"
	}

	"\n[half]\![*]\q[Go Back,OnSettings]"
}

OnNotifyFontInfo
{
	avalibleFonts = reference
}

OnMailSize
{
	max[0] = reference0
	OnSettings
}

OnStoryMode
{
	mode = reference0
	OnSettings
}

OnFontSize
{
	if reference0 == "-" ; fontSize -= 1
	elseif reference0 == "+"; fontSize += 1
	else; fontSize = 0

	OnSettings
}

OnFontChange
{
	font = reference0
	OnFontMenu
}

OnMail:all
{
	openMail = TOINT(reference0)
	"\p[2]\s[%(TOINT(reference0) + 100)]"
	if openMail == 1 || openMail == 2 || openMail == 4
	{
		"\i[%(story[openMail])]"
	}
	else
	{
		"\i[0]"
	}
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnSettings
}

EnvelopeTest:all
{
	for _i = 1; _i < 7; _i++
	{
		"\__q[OnMail,%(_i)]\_b[img/letter (%(_i)).png,inline,--option=use_self_alpha]\__q"
		if _i%5==0; "\n"
	}
}